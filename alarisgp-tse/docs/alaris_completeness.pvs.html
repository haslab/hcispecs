<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>Source: alaris_completeness.pvs</title>
    <script src="scripts/prettify/prettify.js"> </script>
    <script src="scripts/prettify/lang-css.js"> </script>
    <!--[if lt IE 9]>
      <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link type="text/css" rel="stylesheet" href="styles/prettify-tomorrow.css">
    <link type="text/css" rel="stylesheet" href="styles/jsdoc-default.css">
</head>
<body>
<div id="main">
    <h1 class="page-title">Source: alaris_completeness.pvs</h1>
    <section>
        <article>
            <pre class="prettyprint source linenums"><code>%%
% @module alaris_completeness
% @author Michael Harrison and Paolo Masci (QMUL and Newcastle University)
% @date April 2017
% @description The completeness theorems.
%%
alaris_completeness_pattern[
   maxrate  : posnat, %{maxrate: posnat | maxrate > 1000},
   maxinfuse: posnat, %{maxinfuse: posnat |  maxinfuse >= 3000 }, % maxtime = maxinfuse needed for proving maxinfuse_over_time_lemma and vtbi_over_time_lemma
   infusemin: { infusemin: posreal | infusemin <= maxinfuse },
   timeout  : posnat,
   shorttimeout : {shorttimeout: posnat | shorttimeout < timeout},
   maxtime  : { maxtime: posnat | maxtime > timeout AND maxtime >= maxinfuse },
   bat_max : {bat_max: posnat | bat_max+1 < maxtime},
   bat_min: {bat_min: posnat | bat_min < bat_max}
]: THEORY
BEGIN
  ASSUMING
    timeoutLTmaxtime: ASSUMPTION timeout < maxtime
    shorttimeoutLTtimeout: ASSUMPTION shorttimeout < timeout
    maxinfuseVal: ASSUMPTION maxinfuse >= 3000
  ENDASSUMING
  IMPORTING alaris_th[maxrate, maxinfuse, infusemin, timeout, shorttimeout, maxtime, bat_max, bat_min]

% Completeness template
% \all s in S : guard(s) & ~goal(s)
% => \exists a in A & per(a)(s) & goal(a(s))

%%
  % @function paused_state
  % @description predicate that is true when the device is in the paused Programmable state
  % @param {state} st The state of the system
  % @returns {bool} True when state paused
  % @memberof module:alaris_completeness
  %%
paused_state(st: state): boolean =
         (device(st)`powered_on? AND NOT device(st)`infusing?)

%%
  % @function infuse_state
  % @description predicate that is true when the device is in the infusing state
  % @param {state} st The state of the system
  % @returns {bool} True when state infusing
  % @memberof module:alaris_completeness
  %%
infuse_state(st: state): boolean =
         (device(st)`powered_on? AND device(st)`infusing?)

% used in the induction

%%
  % @function state_transitions
  % @description predicate that is true when the post state can be derived from the post state using the available actions
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_completeness
  %%
state_transitions(pre, post: state): boolean =
  (per_sup(pre) & post = sup(pre))
  OR (per_release_sup(pre) & post = release_sup(pre))
  OR (per_fup(pre) & post = fup(pre))
  OR (per_release_fup(pre) & post = release_fup(pre))
  OR (per_sdown(pre) & post = sdown(pre))
  OR (per_release_sdown(pre) & post = release_sdown(pre))
  OR (per_fdown(pre) & post = fdown(pre))
  OR (per_release_fdown(pre) & post = release_fdown(pre))
  OR (per_tick(pre) & post = tick(pre))
  OR (per_key1(pre) & post = key1(pre))
  OR (per_key2(pre) & post = key2(pre))
  OR (per_key3(pre) & post = key3(pre))
  OR (per_query(pre) & post = query(pre))
  OR (per_on(pre) & post = on(pre))
  OR (per_run(pre) & post = run(pre))
  OR (per_pause(pre) & post = pause(pre))
  OR (per_switch(pre) & post = switch(pre))
  OR (per_fit_set(pre) & post = fit_set(pre))


% used in inductions relating to chevron functions, including release

%%
  % @function state_transitions_release
  % @description predicate that is true when the post state can be derived from the post state using the available actions, except that a chevron key is always combined with a release
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_completeness
  %%
state_transitions_release(pre, post: state): boolean =
  (per_sup(pre) & post = release_sup(sup(pre))) OR
  (per_fup(pre) & post = release_fup(fup(pre))) OR
  (per_sdown(pre) & post = release_sdown(sdown(pre))) OR
  (per_fdown(pre) & post = release_fdown(fdown(pre))) OR
  (per_tick(pre) & post = tick(pre)) OR
  (per_key1(pre) & post = key1(pre)) OR  (per_key2(pre) & post = key2(pre)) OR (per_key3(pre) & post = key3(pre)) OR
  (per_query(pre) & post = query(pre)) OR
  (per_on(pre) & post = on(pre)) OR
  (per_run(pre) & post = run(pre)) OR  (per_pause(pre) & no_button_down(pre) & post = pause(pre)) OR
  (post = pre) OR (per_switch(pre) & post = switch(pre)) OR
   (per_fit_set(pre) & post = fit_set(pre))

% Completeness template
% A set of actions
% for all s in S : guard(s) & ~goal(s) =>
% exists a in A & per(a)(s) & goal(a(s))

%%
  % @function goal_infuse
  % @description predicate that is true when the entry mode is infusemode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
goal_infuse(st: state): boolean = entrymode(st) = infusemode

%%
  % @function goal_infuse_line
  % @description predicate that is true when the top line shows a display associated with infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
goal_infuse_line(st: state): boolean =
       (topline(st) = infusing) OR (topline(st) = dispkvo)

%%
  % @function guard_infuse
  % @description predicate that is true when the device is infusing, the top line is not showing locked and the device is not showing VTBI in the top line in bag mode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
guard_infuse(st: state): boolean =
   infuse_state(st) AND
   ((topline(st) /= locked) AND
  NOT ((topline(st) = dispvtbi) AND
    (entrymode(st) = bagmode)))

%%
  % @function state_nokey13_transitions_release
  % @description predicate that is true when the post state can be derived from the post state using the available actions, except that a chevron key is always combined with a release and key1 and key3 are omitted
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_completeness
  %%
state_nokey13_transitions_release(pre, post: state): boolean =
    (per_sup(pre) & post = release_sup(sup(pre))) OR
  (per_fup(pre) & post = release_fup(fup(pre))) OR
  (per_sdown(pre) & post = release_sdown(sdown(pre))) OR
  (per_fdown(pre) & post = release_fdown(fdown(pre))) OR
  (per_tick(pre) & post = tick(pre)) OR
  (per_key2(pre) & post = key2(pre)) OR
  (per_query(pre) & post = query(pre)) OR
  (per_run(pre) & post = run(pre)) OR  (per_pause(pre) & post = pause(pre)) OR (post = pre)

%%
  % @function simple_guard_rmode
  % @description predicate that is true when the device is in paused state
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
simple_guard_rmode(st: state): boolean = paused_state(st)

%%
  % @function simple_guard_hosr
  % @description predicate that is true when the device is in paused state
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
simple_guard_hosr(st: state): boolean = paused_state(st)

%%
  % @function guard_rmode
  % @description predicate that is true when the device is in paused state and not locked and not in bag mode (Section 6.1 templates paper)
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
guard_rmode(st: state): boolean = paused_state(st) & ((topline(st) /= locked) AND (topline(st) /= lockrate) AND NOT((topline(st) = dispvtbi) AND ((entrymode(st) = bagmode) OR (entrymode(st) = tbagmode))))
%%
  % @function guard_hosr
  % @description predicate that is true when the device is in paused state and not locked and not in bag mode (Section 6.1 templates paper)
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
guard_hosr(st: state): boolean = paused_state(st) & ((topline(st) /= locked) AND (topline(st) /= lockrate) AND NOT((topline(st) = dispvtbi) AND ((entrymode(st) = bagmode) OR (entrymode(st) = tbagmode))))

%%
  % @function goal_rmode
  % @description predicate that is true when entrymode = rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
goal_rmode(st: state): boolean = (entrymode(st) = rmode)

%%
  % @function goal_hosr
  % @description predicate that is true when the device shows a top line of holding or set rate
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
goal_hosr(st: state): boolean = ((topline(st) = holding) OR
                                 (topline(st) = setrate))

%%
  % @function simple_completeness_rmode
  % @description predicate that is true if key1 or key3 lead to the rmode goal
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
simple_completeness_rmode(st: state): boolean =
    (simple_guard_rmode(st) AND NOT goal_rmode(st)) IMPLIES
         ((per_key1(st) AND goal_rmode(key1(st))) OR
	 (per_key3(st) AND goal_rmode(key3(st))))

%%
  % @function simple_complete_to_hosr
  % @description predicate that is true when both key1 and key3 lead to holding or setrate tope line
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
simple_complete_to_hosr(st: state): boolean =
    (simple_guard_hosr(st) AND NOT goal_hosr(st)) IMPLIES
         ((per_key1(st) AND goal_hosr(key1(st))) OR
	 (per_key3(st) AND goal_hosr(key3(st))))
%%
  % @function comple_to_rmode
  % @description predicate that when key1 and key3 both lead to rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
comple_to_rmode(st: state): boolean =
      (guard_rmode(st) AND NOT goal_rmode(st)) IMPLIES
         ((per_key1(st) AND goal_rmode(key1(st))) OR
	 (per_key3(st) AND goal_rmode(key3(st))))

%%
  % @function complete_to_hosr
  % @description predicate that when key1 and key3 both lead to holding or setrate top lines
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
complete_to_hosr(st: state): boolean =
      (guard_rmode(st) AND NOT goal_hosr(st)) IMPLIES
         ((per_key1(st) AND goal_hosr(key1(st))) OR
	 (per_key3(st) AND goal_hosr(key3(st))))

%%
  % @function comple_to_infusemode
  % @description predicate that key3 both lead to infusemode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
comple_to_infusemode(st: state): boolean =
      (guard_infuse(st) AND NOT goal_infuse(st)) IMPLIES
	 (per_key3(st) => goal_infuse(key3(st)))

%%
  % @function complete_to_infuse
  % @description predicate that key3 both lead to top line of infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
complete_to_infuse(st: state): boolean =
      (guard_infuse(st) AND NOT goal_infuse_line(st)) IMPLIES
	 (per_key3(st) AND goal_infuse_line(key3(st)))

%%
% @theorem simple_comp_rmode
% @description theorem: transition via key1 or key3 to rmode with simple guards
% @yields counter-example used in theorem below
%%
simple_comp_rmode: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => simple_completeness_rmode(post)) AND
     ((state_transitions_release(pre, post) AND
        simple_completeness_rmode(pre))  => simple_completeness_rmode(post))

%%
% @theorem simple_comp_pause
% @description theorem: transition via key1 or key3 to holding or set rate top line
% @yields counter-example below
%%
simple_comp_pause: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => simple_complete_to_hosr(post)) AND
     ((state_transitions_release(pre, post) AND
        simple_complete_to_hosr(pre))  => simple_complete_to_hosr(post))

%simple_comp_pause.2.6.1.5 :
%
%{-1}  ((topline(pre!1) = options) AND
%        (fndisp1(pre!1) = fok) AND (entrymode(pre!1) = qmode))
%[-2]  device(pre!1)`powered_on?
%[-3]  no_button_down(pre!1)
%[-4]  post!1 = key1(pre!1)
%[-5]  simple_complete_to_hosr(pre!1)
%  |-------
%[1]   device(pre!1)`infusing?
%[2]   simple_complete_to_hosr(post!1)

% Trying repeated skolemization, instantiation, and if-lifting,
%this yields  8 subgoals:
%simple_comp_pause.2.6.1.5.1 :
%
%{-1}  options?(topline(pre!1))
%{-2}  fok?(fndisp1(pre!1))
%{-3}  qmode?(entrymode(pre!1))
%[-4]  device(pre!1)`powered_on?
%{-5}  nob?(which_press(pre!1))
%{-6}  pressed(pre!1) = 5
%{-7}  setvtbiovertime?(optionsmenu(pre!1)(qcursor(pre!1)))
%{-8}  rlock(pre!1)
%{-9}  post!1 =
%       pre!1
%         WITH [topline := locked,
%               middisp
%                 := LAMBDA (x:
%                              imid_type
%                                  [maxrate, maxinfuse, infusemin, timeout,
%                                   shorttimeout, maxtime, bat_max,
%                                   bat_min]):
%                      FALSE,
%               fndisp1 := fnull,
%               fndisp2 := fnull,
%               fndisp3 := fnull,
%               entrymode := qmode]
%{-10} fquit?(fndisp3(pre!1))
%  |-------
%[1]   device(pre!1)`infusing?
%{2}   (device(pre!1)`infusionrate = 0)



%%
% @theorem completemplate_to_rmode
% @description theorem: transition via key1 or key3 to rmode, guard deals with counter examples
% @yields QED
% @stats Run time  = 3104.03 secs. 18/7/17
%%
completemplate_to_rmode: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => comple_to_rmode(post)) AND
     ((state_transitions_release(pre, post) AND
        comple_to_rmode(pre))  => comple_to_rmode(post))

%%%
% @theorem univ_complete_to_holdingorsetrate
% @description theorem: transition via key1 or key3 to holding or set rate: guard that in rmode
% @yields generates counter-examples used in future theorems
%
%%
univ_complete_to_holdingorsetrate: THEOREM
  FORALL (st: state):
    (guard_rmode(st) AND NOT goal_hosr(st)) IMPLIES
         ((per_key1(st) AND goal_hosr(key1(st))) OR
	 (per_key3(st) AND goal_hosr(key3(st))))

%%
% @theorem complete_to_pause
% @description theorem: transition via key1 or key3 to top line display of holding or set rate, guard deals with counter examples: used in paper
% @yields QED
% @stats Run time  = 4243.84 secs. 20/9/17
%%
complete_to_pause: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => complete_to_hosr(post)) AND
     ((state_transitions_release(pre, post) AND
        complete_to_hosr(pre))  => complete_to_hosr(post))

%%
% @theorem no_other_to_rmode
% @description theorem: this theorem proves that no other action takes the device to rmode when holding, subject to constraint relating to timeout.
% @yields QED
% @stats Run time  = 87.02 secs. 18/7/17
%%
no_other_to_rmode: THEOREM
  FORALL (pre, post: state):
     (paused_state(pre) AND state_nokey13_transitions_release(pre, post) AND
      entrymode(pre) /= rmode AND (device(pre)`elapse < shorttimeout) AND
      (topline(pre) /= volume)) IMPLIES entrymode(post) /= rmode

%%
% @theorem completetemplate_to_infusemode
% @description theorem: this theorem proves that key3 takes to infusemode if the pump is infusing
% @yields QED
% @stats Run time  = 851.91 secs. 18/7/17
%%
completemplate_to_infusemode: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => comple_to_infusemode(post)) AND
     ((state_transitions_release(pre, post) AND
        comple_to_infusemode(pre))  => comple_to_infusemode(post))

%%
% @theorem complete_to_infuse_thm
% @description theorem: this theorem proves that key3 takes to topline of infusing if the pump device is infusing
% @yields QED
% @stats Run time  = 1244.50 secs. 18/7/17
%%
complete_to_infuse_thm: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => complete_to_infuse(post)) AND
     ((state_transitions_release(pre, post) AND
        complete_to_infuse(pre))  => complete_to_infuse(post))


%%
  % @function guard_vtmode
  % @description this guard relates to completeness to vtmode, requires topline to be holding, setrate or infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
guard_vtmode(st: state): boolean =
(device(st)`powered_on? & ((topline(st) = holding) OR (topline(st) = setrate) OR
                          (topline(st) = infusing)))

%%
  % @function goal_vtmode
  % @description the goal is that it reaches vtmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
goal_vtmode(st: state): boolean = entrymode(st) = vtmode

%%
  % @function comple_to_vtmode
  % @description if the guard is true and the goal is false then can reach the goal using key3
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_completeness
  %%
comple_to_vtmode(st: state): boolean =
      (guard_vtmode(st) AND NOT goal_vtmode(st)) IMPLIES
	 (per_key3(st) => goal_vtmode(key3(st)))

%%
% @theorem complete_vtmode_thm
% @description theorem: this theorem proves that key3 takes to vtmode given the defined guard
% @yields QED
% @stats Run time  = 213.88 secs. 18/7/17
%%
complete_vtmode_thm: THEOREM
  FORALL (pre, post: state):
    (init?(pre) AND post = on(pre) => comple_to_vtmode(post)) AND
     ((state_transitions_release(pre, post) AND
        comple_to_vtmode(pre))  => comple_to_vtmode(post))

END alaris_completeness_pattern</code></pre>
        </article>
    </section>
</div>
<nav>
    <h2><a href="index.html">Alaris GP</a></h2>
    <h3>Modules</h3>
    <ul>
        <li><a href="module-alaris_th.html">alaris_th</a></li>
        <li><a href="module-pump_th.html">pump_th</a></li>
        <li><a href="module-types_and_constants_th.html">types_and_constants_th</a></li>
    </ul>
    <h3>Theorems</h3>
    <ul>
        <li><a href="theorems-alaris_completeness.html">alaris_completeness</a></li>
        <li><a href="theorems-alaris_consistency.html">alaris_consistency</a></li>
        <li><a href="theorems-alaris_feedback.html">alaris_feedback</a></li>
        <li><a href="theorems-alaris_reversibility.html">alaris_reversibility</a></li>
        <li><a href="theorems-alaris_universality.html">alaris_universality</a></li>
        <li><a href="theorems-alaris_visibility.html">alaris_visibility</a></li>
    <ul>
</nav>
<br class="clear">
<footer>
    Documentation generated by <a href="https://github.com/jsdoc3/jsdoc">JSDoc 3.6.0-dev</a> on Sat Aug 26 2017 20:26:54 GMT+0100 (WEST)
</footer>
<script> prettyPrint(); </script>
<script src="scripts/linenumber.js"> </script>
</body>
</html>
