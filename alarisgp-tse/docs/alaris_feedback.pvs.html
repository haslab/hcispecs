<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>Source: alaris_feedback.pvs</title>

    <script src="scripts/prettify/prettify.js"> </script>
    <script src="scripts/prettify/lang-css.js"> </script>
    <!--[if lt IE 9]>
      <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link type="text/css" rel="stylesheet" href="styles/prettify-tomorrow.css">
    <link type="text/css" rel="stylesheet" href="styles/jsdoc-default.css">
</head>
<body>
<div id="main">
    <h1 class="page-title">Source: alaris_feedback.pvs</h1>
    <section>
        <article>
            <pre class="prettyprint source linenums"><code>%%
% @module alaris_feedback
% @author Michael Harrison and Paolo Masci (QMUL and Newcastle University)
% @date April 2017
% @description The feedback theorems
%%

% State feedback
% FORALL  s1, s2 in S, guard(s1) AND guard(s2) AND
% filter(s1) \neq filter(s2)
% \implies filter_p(s1) \neq filter_p(s2)

% State feedback
% FORALL  s1, s2 in S, guard(s1) AND guard(s2) AND
% filter(s1) \neq filter(s2)
% \implies visible_change_filter(s1, s2)

% Action feedback
% FORALL a in S -> S, FORALL s in S : per(a)(s) AND
% guard(s) AND filter(s) \neq filter(a(s)) IMPLIES
% p_filter(s) \neq p_filter(a(s))


alaris_feedback_pattern[
   maxrate  : posnat, %{maxrate: posnat | maxrate > 1000},
   maxinfuse: posnat, %{maxinfuse: posnat |  maxinfuse >= 3000 }, % maxtime = maxinfuse needed for proving maxinfuse_over_time_lemma and vtbi_over_time_lemma
   infusemin: { infusemin: posreal | infusemin <= maxinfuse },
   timeout  : posnat,
   shorttimeout : {shorttimeout: posnat | shorttimeout < timeout},
   maxtime  : { maxtime: posnat | maxtime > timeout AND maxtime >= maxinfuse },
   bat_max : {bat_max: posnat | bat_max+1 < maxtime},
   bat_min: {bat_min: posnat | bat_min < bat_max}
]: THEORY
BEGIN
  ASSUMING
    timeoutLTmaxtime: ASSUMPTION timeout < maxtime
    shorttimeoutLTtimeout: ASSUMPTION shorttimeout < timeout
    maxinfuseVal: ASSUMPTION maxinfuse >= 3000
  ENDASSUMING
  IMPORTING alaris_th[maxrate, maxinfuse, infusemin, timeout, shorttimeout, maxtime, bat_max, bat_min]

%%
  % @function state_transitions
  % @description predicate that is true when the post state can be derived from the post state using the available actions
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_feedback
  %%
state_transitions(pre, post: state): boolean =
  (per_sup(pre) & post = sup(pre))
  OR (per_release_sup(pre) & post = release_sup(pre))
  OR (per_fup(pre) & post = fup(pre))
  OR (per_release_fup(pre) & post = release_fup(pre))
  OR (per_sdown(pre) & post = sdown(pre))
  OR (per_release_sdown(pre) & post = release_sdown(pre))
  OR (per_fdown(pre) & post = fdown(pre))
  OR (per_release_fdown(pre) & post = release_fdown(pre))
  OR (per_tick(pre) & post = tick(pre))
  OR (per_key1(pre) & post = key1(pre))
  OR (per_key2(pre) & post = key2(pre))
  OR (per_key3(pre) & post = key3(pre))
  OR (per_query(pre) & post = query(pre))
  OR (per_on(pre) & post = on(pre))
  OR (per_run(pre) & post = run(pre))
  OR (per_pause(pre) & post = pause(pre))
  OR (per_switch(pre) & post = switch(pre))
  OR (per_fit_set(pre) & post = fit_set(pre))

%%
  % @function state_transitions_noton
  % @description predicate that is true when the post state can be derived from the post state using the available actions, with the exception of on
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_feedback
  %%
state_transitions_noton(pre, post: state): boolean =
  (per_sup(pre) & post = sup(pre)) OR
  (per_release_sup(pre) & post = release_sup(pre)) OR
  (per_fup(pre) & post = fup(pre)) OR
(per_release_fup(pre) & post = release_fup(pre)) OR
 (per_sdown(pre) & post = sdown(pre)) OR
(per_release_sdown(pre) & post = release_sdown(pre)) OR
  (per_fdown(pre) & post = fdown(pre)) OR
(per_release_fdown(pre) & post = release_fdown(pre)) OR
(per_tick(pre) & post = tick(pre)) OR
  (per_key1(pre) & post = key1(pre)) OR
(per_key2(pre) & post = key2(pre)) OR
(per_key3(pre) & post = key3(pre)) OR
  (per_query(pre) & post = query(pre)) OR
(per_run(pre) & post = run(pre)) OR  (per_pause(pre) & post = pause(pre)) OR
(per_switch(pre) & post = switch(pre)) OR
(per_fit_set(pre) & post = fit_set(pre))

%%
  % @function state_transitions_nochevrons
  % @description predicate that is true when the post state can be derived from the post state using the available actions, but not the chevron keys
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_feedback
  %%
state_transitions_nochevrons(pre, post: state): boolean =
  (per_key1(pre) & post = key1(pre)) OR
(per_key2(pre) & post = key2(pre)) OR
(per_key3(pre) & post = key3(pre)) OR
  (per_query(pre) & post = query(pre)) OR
  (per_run(pre) & post = run(pre)) OR  (per_pause(pre) & post = pause(pre))

%%
  % @function filter_rate
  % @description extract the infusion rate parameter from the state
  % @param {state} st The state of the system
  % @returns {irates}
  % @memberof module:alaris_feedback
  %%
filter_rate(st: state): irates = device(st)`infusionrate

%%
  % @function vis_filter_rate
  % @description predicate that is true if infusion pump parameter is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_rate(st: state): boolean = middisp(st)(drate)

%%
  % @function guard_vis_rate
  % @description predicate that excludes top line of vtbi over time because temporary rate shown
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_vis_rate(st: state): boolean =
    NOT(topline(st) = vtbitime)

%%
% @theorem simple_feedback_rate_theorem
% @description instantiation of feedback template without guard, used as example in paper
% @yields counter-example below
% @stats
%%
%{-1}  device(pre!1)`powered_on?
%{-2}  nob?(which_press(pre!1))
%{-3}  pressed(pre!1) = 5
%{-4}  fok?(fndisp1(pre!1))
%{-5}  middisp(pre!1)(7)
%{-6}  vtbitime?(topline(pre!1))
%{-7}  ttmode?(entrymode(pre!1))
%{-8}  middisp(pre!1)(9)
%{-9}  middisp(pre!1)(8)   %% dnewrate = 9
%{-10} newtime(pre!1) = 0
%{-11} post!1 =
%       pre!1
%         WITH [topline := setrate,
%               middisp
%                 := LAMBDA (x:
%                              imid_type
%                                  [maxrate, maxinfuse, infusemin, timeout,
%                                   shorttimeout, maxtime, bat_max,
%                                   bat_min]):
%                      (x = 0) OR (x = 1) OR (x = 3) OR (x = 2), %% drate = 0
%               fndisp1 := fvol,
%               fndisp2 := fvtbi,
%               fndisp3 := fnull,
%               entrymode := rmode,
%               device
%                 := device(pre!1)
%                      WITH [infusionrate := 0,
%                            time := 0,
%                            vtbi := newvtbi(pre!1),
%                            elapse := 0]]
%  |-------
%{1}   device(pre!1)`infusing?
%{2}   device(pre!1)`infusionrate = 0
%{3}   middisp(pre!1)(0)
simple_feedback_rate_theorem: THEOREM
   FORALL (pre, post: state):
      (state_transitions(pre, post) AND
      filter_rate(pre) /= filter_rate(post)) =>
         vis_filter_rate(pre) AND vis_filter_rate(post)



%%
% @theorem feedback_rate_theorem
% @description instantiation of feedback template with guard
% @yields QED
% @stats Run time  = 131.07 secs. 18/7/17
%%
feedback_rate_theorem: THEOREM
   FORALL (pre, post: state):
      (state_transitions(pre, post) AND
      guard_vis_rate(pre) AND
      guard_vis_rate(post) AND
      filter_rate(pre) /= filter_rate(post)) =>
         vis_filter_rate(pre) AND vis_filter_rate(post)

%%
  % @function filter_vtbi
  % @description extract the vtbi parameter from the state
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_vtbi(st: state): ivols = device(st)`vtbi

%%
  % @function vis_filter_vtbi
  % @description predicate that is true if infusion pump parameter or the temporary value  is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_vtbi(st: state): boolean = middisp(st)(dvtbi) OR middisp(st)(dnewvtbi)

%%
  % @function guard_vis_vtbi
  % @description predicate that excludes a range of top lines
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_vis_vtbi(st: state): boolean =
        (topline(st) /= dispkvo) % shows the kvo value not the rate
	AND (topline(st) /= locked)
	AND (topline(st) /= batterylow)
	AND (topline(st) /= vtbidone)
	AND NOT ((topline(st) = holding OR topline(st) = infusing OR
	          topline(st) = setrate)
	         AND device(st)`vtbi = 0)

%%
% @theorem feedback_vtbi_theorem
% @description instantiation of feedback template with guard
% @yields QED
% @stats Run time  = 477.85 secs. 19/7/17
%%
feedback_vtbi_theorem: THEOREM
   FORALL (pre, post: state):
      (state_transitions(pre, post) AND
      guard_vis_vtbi(pre) AND
      guard_vis_vtbi(post) AND
      filter_vtbi(pre) /= filter_vtbi(post)) =>
         vis_filter_vtbi(pre) AND vis_filter_vtbi(post)


%%
  % @function filter_time
  % @description extract the time parameter from the pump state
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_time(st: state): itimes =
      device(st)`time

%%
  % @function guard_vis_time
  % @description predicate that excludes a range of top lines
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_vis_time(st: state): boolean =
            (topline(st) /= vtbidone) AND
             (topline(st) /= batterylow) AND
	     (topline(st) /= locked) AND
	     NOT ((topline(st) = dispvtbi) AND (entrymode(st) = vtmode)) AND
	     NOT (((topline(st) = dispinfo) OR
	     (topline(st) = options)) AND device(st)`infusing?) AND
	     (device(st)`vtbi /= 0)

%%
  % @function vis_filter_time
  % @description predicate that is true if infusion pump parameter or the temporary value  is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_time(st: state): boolean =
        middisp(st)(dtime) OR middisp(st)(dnewtime)

%%
% @theorem feedback_time_theorem
% @description instantiation of feedback template with guard
% @yields QED
% @stats Run time  = 566.71 secs. 19/7/17
%%
feedback_time_theorem: THEOREM
   FORALL (pre, post: state):
      (state_transitions(pre, post) AND
      guard_vis_time(pre) AND
      guard_vis_time(post) AND
      filter_time(pre) /= filter_time(post)) =>
         vis_filter_time(pre) AND vis_filter_time(post)

%%
  % @function guard_vis_volume
  % @description predicate that excludes a range of top lines
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_vis_volume(st: state): boolean = ((topline(st) /= vtbidone) AND
            (topline(st) /= batterylow) AND (topline(st) /= locked))

%%
  % @function filter_volume
  % @description extract the volume infused parameter from the pump state
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_volume(st: state): ivols = device(st)`volumeinfused

%%
  % @function vis_filter_volume
  % @description predicate that is true if infusion pump parameter is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_volume(st: state): boolean = middisp(st)(dvol)

%%
% @theorem feedback_volume_theorem
% @description instantiation of feedback template with guard
% @yields QED
% @stats Run time  = 444.85 secs. 19/7/17
%%
feedback_volume_theorem: THEOREM
         FORALL (pre, post: state):
          (state_transitions(pre, post) AND guard_vis_volume(pre) AND
          guard_vis_volume(post) AND
          (filter_volume(pre) /= filter_volume(post))) =>
           vis_filter_volume(pre) AND vis_filter_volume(post)
%
%%
  % @function filter_newrate
  % @description extract the temporary infusion rate from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_newrate(st: state): irates = newrate(st)

%%
  % @function filter_newvtbi
  % @description extract the temporary vtbi from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_newvtbi(st: state): ivols = newvtbi(st)

%%
  % @function filter_newtime
  % @description extract the temporary time from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_newtime(st: state): itimes = newtime(st)

%%
  % @function vis_filter_newrate
  % @description predicate that is true if temporary parameter is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_newrate(st: state): boolean = middisp(st)(dnewrate)

%%
  % @function vis_filter_newvtbi
  % @description predicate that is true if the temporary infusion pump parameter is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_newvtbi(st: state): boolean = middisp(st)(dnewvtbi)

%%
  % @function vis_filter_newtime
  % @description predicate that is true if the temporary infusion pump parameter is visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_filter_newtime(st: state): boolean = middisp(st)(dnewtime)

%%
% @theorem feedback_newrate_theorem
% @description instantiation of feedback template without guard, and no visibility constraint on state before action
% @yields QED
% @stats Run time  = 127.05 secs. 19/7/17
%%
feedback_newrate_theorem: THEOREM
         FORALL (pre, post: state):
          (state_transitions(pre, post) AND
          (filter_newrate(pre) /= filter_newrate(post))) =>
           vis_filter_newrate(post)

%%
% @theorem feedback_newvtbi_theorem
% @description instantiation of feedback template without guard, and no visibility constraint on state before action
% @yields QED
% @stats Run time  = 126.58 secs. 19/7/17
%%
feedback_newvtbi_theorem: THEOREM
         FORALL (pre, post: state):
          (state_transitions(pre, post) AND
          (filter_newvtbi(pre) /= filter_newvtbi(post))) =>
           vis_filter_newvtbi(post)

%%
% @theorem feedback_newtime_theorem
% @description instantiation of feedback template without guard, and no visibility constraint on state before action
% @yields QED
% @stats Run time  = 122.92 secs. 19/7/17
%%
feedback_newtime_theorem: THEOREM
         FORALL (pre, post: state):
          (state_transitions(pre, post) AND
          (filter_newtime(pre) /= filter_newtime(post))) =>
           vis_filter_newtime(post)


%%
  % @function guard_entrymode
  % @description predicate that excludes top line of vtbi, vtbi over time, batterylow
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_entrymode(st: state): boolean =
            NOT((topline(st) = dispvtbi) OR (topline(st) = vtbitime) OR
                                   (topline(st) = batterylow))

%%
  % @function filter_entrymode
  % @description extract the entrymode from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_feedback
  %%
filter_entrymode(st: state): emodes = entrymode(st)
%%
  % @function p_filter_entrymode
  % @description extracts the top line
  % @param {state} st The state of the system
  % @returns {iline}
  % @memberof module:alaris_feedback
  %%
p_filter_entrymode(st: state): iline = topline(st)

%%
% @theorem feedback_entrymode_theorem
% @description instantiation of feedback template
% @yields QED
% @stats Run time  = 625.14 secs. 19/7/17
%%
feedback_entrymode_theorem: THEOREM
         FORALL (pre, post: state):
          (state_transitions_noton(pre, post) AND guard_entrymode(pre) AND
          guard_entrymode(post) AND
          (filter_entrymode(pre) /= filter_entrymode(post))) =>
           (p_filter_entrymode(pre) /= p_filter_entrymode(post))


%%
  % @function guard_entrymode_nochevrons
  % @description predicate that excludes top line of vtbi, vtbi over time
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_entrymode_nochevrons(st: state): boolean = NOT((topline(st) = dispvtbi) OR (topline(st) = vtbitime))

%%
% @theorem feedback_entrymode_theorem_nochevrons
% @description instantiation of action feedback template without state filter
% @yields QED
% @stats Run time  = 57.90 secs. 19/7/17
% could also be considered consistency
%%
feedback_entrymode_theorem_nochevrons: THEOREM
         FORALL (pre, post: state):
          (state_transitions_nochevrons(pre, post) AND guard_entrymode_nochevrons(pre) AND
          guard_entrymode_nochevrons(post)) =>
           (p_filter_entrymode(pre) /= p_filter_entrymode(post))


%%
  % @function guard_switch
  % @description switch is permitted
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_switch(st: state): boolean = per_switch(st)

%%
% @theorem action_feedback_switch
% @description p_filter defined in terms of battery_light and ac_light
% @yields QED
% @stats Run time  = 0.29 secs. 20/7/17
%%
action_feedback_switch: THEOREM
FORALL (st: state):
     guard_switch(st) IMPLIES
          ((battery_light(st) /= battery_light(switch(st))) AND
	  (ac_light(st) /= ac_light(switch(st))))

% the following two theorems are not instances of templates

%%
% @theorem tick_infusionrate_ge_vtbi
% @description This proves that when VTBI is exhausted, tick is permitted, mains is connected and KVO mode is not set then topline shows vtbidone and the KVO mode is set
% @yields QED
% @stats Run time  = 0.47 secs. 19/7/17
%%
tick_infusionrate_ge_vtbi: THEOREM
       FORALL (st: state):
           (device(st)`infusionrate >= device(st)`vtbi &
	   per_tick(st) &
	   NOT device(st)`kvoflag &
             device(st)`infusing? & device(st)`ac_connect)
          IMPLIES
	   (LET stprime = tick(st) IN
             (topline(stprime) = vtbidone & device(stprime)`kvoflag))

%%
% @theorem tick_infusionrate_ge_vtbi_kvoflag
% @description this specific theorem shows that when VTBI is exhausted, KVO mode is set, the pump is infusing and mains is connected then a tick does not change the top line and keeps KVO mode
% @yields QED
% @stats Run time  = 0.46 secs. 20/7/17
%%
tick_infusionrate_ge_vtbi_kvoflag: THEOREM
       FORALL (st: state):
           (per_tick(st) AND
           (device(st)`infusionrate >= device(st)`vtbi) & device(st)`kvoflag &
             device(st)`infusing? & device(st)`ac_connect)
          IMPLIES
              LET stprime = tick(st) IN
	        (topline(stprime) = topline(st) & device(stprime)`kvoflag)

% General tick feedback
%%
  % @function guard_tick
  % @description predicate that requires infusing without KVO mode, mains connected and topline not vtbidone
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_tick(st: state): boolean = device(st)`infusing? AND NOT device(st)`kvoflag AND
                               device(st)`ac_connect AND topline(st) /= vtbidone

%%
  % @function vis_tick
  % @description predicate that is true if infusion rate, time and vtbi are visible
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%

vis_tick(st: state): boolean = middisp(st)(drate) AND middisp(st)(dtime) AND middisp(st)(dvtbi)
%%
% @theorem action_feedback_tick
% @description shows that if tick is permitted and the guard is true of initial state and the state after the action has occurred then the effect is visible.
% @yields QED
% @stats Run time  = 1.55 secs. 20/7/17
%%
action_feedback_tick: THEOREM
    FORALL (st: state):
       (per_tick(st) AND guard_tick(st) AND guard_tick(tick(st))) IMPLIES
               vis_tick(tick(st))

%%
  % @function guard_tick_vtbidone
  % @description predicate that requires vtbi exhausted, not KVO, infusing, mains connected and topline showing infusing
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
guard_tick_vtbidone(st: state): boolean =
   device(st)`infusionrate >= device(st)`vtbi AND
   NOT device(st)`kvoflag AND device(st)`infusing? AND
   device(st)`ac_connect AND topline(st) = infusing

%%
  % @function vis_vtbidone
  % @description predicate that is true if topline shows vtbidone
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_feedback
  %%
vis_vtbidone(st: state): boolean = topline(st) = vtbidone

%%
% @theorem action_feedback_tick_vtbidone
% @description shows that if tick is permitted and the guard is true of initial state then the topline shows vtbidone
% @yields QED
% @stats Run time  = 0.93 secs. 2/8/17
%%
action_feedback_tick_vtbidone: THEOREM
    FORALL (st: state):
       (per_tick(st) AND guard_tick_vtbidone(st)) IMPLIES
               vis_vtbidone(tick(st))


END alaris_feedback_pattern</code></pre>
        </article>
    </section>
</div>
<nav>
    <h2><a href="index.html">Alaris GP</a></h2>
    <h3>Modules</h3>
    <ul>
        <li><a href="module-alaris_th.html">alaris_th</a></li>
        <li><a href="module-pump_th.html">pump_th</a></li>
        <li><a href="module-types_and_constants_th.html">types_and_constants_th</a></li>
    </ul>
    <h3>Theorems</h3>
    <ul>
        <li><a href="theorems-alaris_completeness.html">alaris_completeness</a></li>
        <li><a href="theorems-alaris_consistency.html">alaris_consistency</a></li>
        <li><a href="theorems-alaris_feedback.html">alaris_feedback</a></li>
        <li><a href="theorems-alaris_reversibility.html">alaris_reversibility</a></li>
        <li><a href="theorems-alaris_universality.html">alaris_universality</a></li>
        <li><a href="theorems-alaris_visibility.html">alaris_visibility</a></li>
    <ul>
</nav>
<br class="clear">
<footer>
    Documentation generated by <a href="https://github.com/jsdoc3/jsdoc">JSDoc 3.6.0-dev</a> on Sat Aug 26 2017 19:49:49 GMT+0100 (WEST)
</footer>
<script> prettyPrint(); </script>
<script src="scripts/linenumber.js"> </script>
</body>
</html>
