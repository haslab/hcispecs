<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>Source: alaris_consistency.pvs</title>
    <script src="scripts/prettify/prettify.js"> </script>
    <script src="scripts/prettify/lang-css.js"> </script>
    <!--[if lt IE 9]>
      <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link type="text/css" rel="stylesheet" href="styles/prettify-tomorrow.css">
    <link type="text/css" rel="stylesheet" href="styles/jsdoc-default.css">
</head>
<body>
<div id="main">
    <h1 class="page-title">Source: alaris_consistency.js</h1>
    <section>
        <article>
            <pre class="prettyprint source linenums"><code>%%
% @module alaris_consistency
% @author Michael Harrison and Paolo Masci (QMUL and Newcastle University)
% @date April 2017
% @description The consistency theorems
%%
alaris_consistency_pattern[
   maxrate  : posnat, %{maxrate: posnat | maxrate > 1000},
   maxinfuse: posnat, %{maxinfuse: posnat |  maxinfuse >= 3000 }, % maxtime = maxinfuse needed for proving maxinfuse_over_time_lemma and vtbi_over_time_lemma
   infusemin: { infusemin: posreal | infusemin <= maxinfuse },
   timeout  : posnat,
   shorttimeout : {shorttimeout: posnat | shorttimeout < timeout},
   maxtime  : { maxtime: posnat | maxtime > timeout AND maxtime >= maxinfuse },
   bat_max : {bat_max: posnat | bat_max+1 < maxtime},
   bat_min: {bat_min: posnat | bat_min < bat_max}
]: THEORY
BEGIN
  ASSUMING
    timeoutLTmaxtime: ASSUMPTION timeout < maxtime
    shorttimeoutLTtimeout: ASSUMPTION shorttimeout < timeout
    maxinfuseVal: ASSUMPTION maxinfuse >= 3000
  ENDASSUMING
  IMPORTING alaris_th[maxrate, maxinfuse, infusemin, timeout, shorttimeout, maxtime, bat_max, bat_min]

% Consistency Template
% A_c is a set of actions \subseteq S -> S
% \forall a in A_c, s in S, m in MS
% guard(s, m) =>
% pre_filter(s, m) R post_filter(a(s), m)
% state consistency
% \forall s in S: guard(s) => invariant(s)

%%
  % @function paused_state
  % @description predicate that is true when the device is in the paused Programmable state
  % @param {state} st The state of the system
  % @returns {bool} True when state paused
  % @memberof module:alaris_consistency
  %%
paused_state(st: state): boolean =
         (device(st)`powered_on? AND NOT device(st)`infusing?)

%%
  % @function state_transitions
  % @description predicate that is true when the post state can be derived from the post state using the available actions
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_consistency
  %%
state_transitions(pre, post: state): boolean =
  (per_sup(pre) & post = sup(pre))
  OR (per_release_sup(pre) & post = release_sup(pre))
  OR (per_fup(pre) & post = fup(pre))
  OR (per_release_fup(pre) & post = release_fup(pre))
  OR (per_sdown(pre) & post = sdown(pre))
  OR (per_release_sdown(pre) & post = release_sdown(pre))
  OR (per_fdown(pre) & post = fdown(pre))
  OR (per_release_fdown(pre) & post = release_fdown(pre))
  OR (per_tick(pre) & post = tick(pre))
  OR (per_key1(pre) & post = key1(pre))
  OR (per_key2(pre) & post = key2(pre))
  OR (per_key3(pre) & post = key3(pre))
  OR (per_query(pre) & post = query(pre))
  OR (per_on(pre) & post = on(pre))
  OR (per_run(pre) & post = run(pre))
  OR (per_pause(pre) & post = pause(pre))
  OR (per_switch(pre) & post = switch(pre))
  OR (per_fit_set(pre) & post = fit_set(pre))

%%
  % @function state_transitions_nochevrons
  % @description predicate that is true when the post state can be derived from the post state using the actions key2, key3, query, run, pause
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_consistency
  %%
state_transitions_nochevrons(pre, post: state): boolean =
%  (per_key1(pre) & post = key1(pre)) OR
  (per_key2(pre) & post = key2(pre)) OR
  (per_key3(pre) & post = key3(pre)) OR
  (per_query(pre) & post = query(pre)) OR
  (per_run(pre) & post = run(pre)) OR  (per_pause(pre) & post = pause(pre))

%%
  % @function state_transitions_nochevrons_or_key1
  % @description predicate that is true when the post state can be derived from the post state using the actions key2, key3, query, tick
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_consistency
  %%
state_transitions_nochevrons_or_key1(pre, post: state): boolean =
  (per_key2(pre) & post = key2(pre)) OR
  (per_key3(pre) & post = key3(pre)) OR
  (per_query(pre) & post = query(pre)) OR
  (per_tick(pre) & post = tick(pre))

%%
  % @function state_transitions_chevrons
  % @description predicate that is true when the post state can be derived from the post state using  the chevron keys
  % @param {state} pre The state of the system
  % @param {state} post The state of the system
  % @returns {bool} True when pre and post states are related
  % @memberof module:alaris_consistency
  %%
state_transitions_chevrons(pre, post: state): boolean =
  (per_sup(pre) & post = sup(pre)) OR
  (per_release_sup(pre) & post = release_sup(pre)) OR
  (per_fup(pre) & post = fup(pre)) OR
  (per_release_fup(pre) & post = release_fup(pre)) OR
  (per_sdown(pre) & post = sdown(pre)) OR
  (per_release_sdown(pre) & post = release_sdown(pre)) OR
  (per_fdown(pre) & post = fdown(pre)) OR
  (per_release_fdown(pre) & post = release_fdown(pre))
%%
  % @function switched on?
  % @description Whether the device is powered on
  % @param {state} st The state of the system
  % @returns {bool} True when powered on
  % @memberof module:alaris_consistency
  %%
switchedon?(p: state): bool =
     device(p)`powered_on?

%%
  % @function rate_entry_ready
  % @description circumstances when ready to enter infusion rate
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_consistency
  %%
rate_entry_ready(st: state): boolean = switchedon?(st) AND NOT rlock(st) AND (((entrymode(st) = rmode) AND (topline(st) = holding)) OR ((entrymode(st) = infusemode) AND (topline(st)=infusing)))

%%
  % @function dispvtbi_entry_ready
  % @description circumstances when ready to enter vtbi, in vtbi over rate
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_consistency
  %%
dispvtbi_entry_ready(st: state): boolean =
                  switchedon?(st) AND
                  (topline(st) = dispvtbi AND entrymode(st) = vtmode)

%%
  % @function vtbitime_entry_ready
  % @description circumstances when ready to enter vtbi, vtbi over time
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_consistency
  %%
vtbitime_entry_ready(st: state): boolean =
                  switchedon?(st) AND
                  NOT device(st)`infusing? AND
                   (topline(st) = vtbitime AND entrymode(st) = vttmode)

%%
  % @function time_entry_ready
  % @description circumstances when ready to enter time, vtbi over time
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_consistency
  %%
time_entry_ready(st: state): boolean =
                  switchedon?(st) AND
                  NOT device(st)`infusing? AND
                   (topline(st) = vtbitime AND entrymode(st) = ttmode)


%%
  % @function filter_entrymode
  % @description extract the entrymode from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
filter_entrymode(st: state): emodes = entrymode(st)

%%
  % @function guard_entrymode
  % @description predicate is true
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_entrymode(st: state): boolean = true

%%
% @theorem consistency_entrymode_theorem_nochevrons
% @description instantiation of consistency template, for all functions without chevrons or key1
% @yields QED
% @stats Run time  = 4.84 secs. 20/7/17
% could also be considered consistency
%%
consistency_entrymode_theorem_nochevrons: THEOREM
         FORALL (pre, post: state):
          (state_transitions_nochevrons(pre, post) AND guard_entrymode(pre)) IMPLIES
           (filter_entrymode(pre) /= filter_entrymode(post))

%%
% @theorem consistency_entrymode_theorem_generalkey
% @description instantiation of consistency template, for all functions without chevrons or key1
% @yields QED
% @stats Run time  = 1.78 secs. 20/7/17
% could also be considered consistency
%%
% pause also changes mode except when infusion rate is locked
consistency_entrymode_theorem_generalkey: THEOREM
  FORALL (st: state):
      (per_pause(st) & (topline(st) /= locked) =>
                filter_entrymode(pause(st)) /= filter_entrymode(st)) AND
      (per_key1(st) & NOT ((topline(st) = options &
              (optionsmenu(st)(qcursor(st)) = setvtbiovertime)) &
                                  rlock(st)) =>
		filter_entrymode(key1(st)) /= filter_entrymode(st))

%%
% @theorem consistency_entrymode_theorem_chevronkeys
% @description instantiation of consistency template, for all chevrons
% @yields QED
% @stats Run time  = 71.27 secs. 20/7/17
% could also be considered consistency
%%
consistency_entrymode_theorem_chevronkeys: THEOREM
         FORALL (pre, post: state):
          (state_transitions_chevrons(pre, post)) IMPLIES
           (filter_entrymode(pre) = filter_entrymode(post))


%%
  % @function guard_ok
  % @description function display 1 is okay, not infusing and excludes three toplines
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_ok(st: state): boolean =
       fndisp1(st) = fok AND NOT device(st)`infusing? AND
           NOT ((topline(st) = options ) OR
                (topline(st) = vtbitime AND (entrymode(st) = vttmode)) OR
                (topline(st) = dispvtbi AND ((entrymode(st) = bagmode) OR
                                             (entrymode(st) = tbagmode))))

%%
  % @function goal_ok
  % @description topline is either holding or set rate depending on value of infusion rate
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
goal_ok(st: state): boolean =
         (device(st)`infusionrate = 0 & topline(st) = setrate) OR
         (device(st)`infusionrate /= 0 & topline(st) = holding)

%%
  % @function ok_goal_consistent
  % @description formulation of consistency peoperty key1 always leads to the ok goal
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
ok_goal_consistent(st: state): boolean =
          (per_key1(st) & guard_ok(st)) IMPLIES
           goal_ok(key1(st))

%%
% @theorem ok_goal_consistent_theorem
% @description uses function above in induction
% @yields QED
% @stats Run time  = 517.03 secs. 20/7/17
%%
ok_goal_consistent_theorem: THEOREM
 FORALL (pre, post: state):
       (init?(pre) =>  ok_goal_consistent(pre)) AND
       ((state_transitions(pre, post) AND  ok_goal_consistent(pre))
           IMPLIES ok_goal_consistent(post))

%%
  % @function guard_quit
  % @description function display 3 is quit
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_quit(st: state): boolean =
       fndisp3(st) = fquit AND NOT device(st)`infusing?

%%
  % @function goal_quit
  % @description topline is either holding or set rate depending on value of infusion rate
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
goal_quit(st: state): boolean =
         (device(st)`infusionrate = 0 & topline(st) = setrate) OR
         (device(st)`infusionrate /= 0 & topline(st) = holding)

%%
  % @function quit_goal_consistent
  % @description formulation of consistency property key3 always leads to the quit goal
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
quit_goal_consistent(st: state): boolean =
          (per_key3(st) & guard_quit(st)) IMPLIES
           goal_quit(key3(st))

%%
% @theorem quit_goal_consistent_theorem
% @description uses function above in induction
% @yields QED
% @stats Run time  = 707.58 secs. 20/7/17
%%
quit_goal_consistent_theorem: THEOREM
   FORALL (pre, post: state):
      (init?(pre) =>  quit_goal_consistent(pre)) AND
       (state_transitions(pre, post) AND  quit_goal_consistent(pre)
           IMPLIES quit_goal_consistent(post))

%%
  % @function guard_init_volume
  % @description key2 permitted and volume in topline
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_init_volume(st: state): boolean =
   per_key2(st) & topline(st) = volume

%%
  % @function goal_init_volume
  % @description volume infused set to zero and return to rmode or infusemode
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
goal_init_volume(st: state): boolean =
  (device(st)`volumeinfused = 0) AND
  ((entrymode(st) = rmode) OR (entrymode(st) = infusemode))

%%
  % @function init_volume_goal_consistent
  % @description formulation of consistency property key2 always leads to the volume init goal
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
init_volume_goal_consistent(st: state): boolean =
  (guard_init_volume(st) IMPLIES goal_init_volume(key2(st)))

%%
% @theorem init_volume_goal_consistent_theorem
% @description uses function above in induction
% @yields QED
% @stats Run time  = 136.26 secs. 20/7/17
%%
init_volume_goal_consistent_theorem: THEOREM
    FORALL (pre, post: state):
       (init?(pre) =>  init_volume_goal_consistent(pre)) AND
       (state_transitions(pre, post) AND  init_volume_goal_consistent(pre)
           IMPLIES init_volume_goal_consistent(post))
%%
  % @function guard_quit_pump_nochange
  % @description key 3 display quit
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%

guard_quit_pump_nochange(st: state): bool = fndisp3(st) = fquit
%%
  % @function filter_rate
  % @description extract the infusion rate parameter from the state
  % @param {state} st The state of the system
  % @returns {irates}
  % @memberof module:alaris_consistency
  %%
filter_rate(st: state): irates = device(st)`infusionrate

%%
  % @function filter_time
  % @description extract the time parameter from the pump state
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%

filter_time(st: state): itimes = device(st)`time
%%
  % @function filter_vtbi
  % @description extract the vtbi parameter from the state
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
filter_vtbi(st: state): ivols = device(st)`vtbi

%%
% @theorem quit_pump_nochange_consistent_theorem
% @description consistently leaves pump parameters unchanged
% @yields QED
% @stats Run time  = 136.26 secs. 20/7/17
%%
quit_pump_nochange_consistent_theorem: THEOREM
  FORALL(st: state):
       (per_key3(st) AND guard_quit_pump_nochange(st)) IMPLIES
              ((filter_rate(st) = filter_rate(key3(st))) AND
              (filter_time(st) = filter_time(key3(st))) AND
              (filter_vtbi(st) = filter_vtbi(key3(st))))


%%
% @theorem pump_parameters_untouched_theorem
% @description consistently leaves pump parameters unchanged
% @yields QED
% @stats Run time  = 16.66 secs. 20/7/17
%%
pump_parameters_untouched_theorem: THEOREM
   FORALL (pre, post: state):
     (paused_state(pre) AND
     (state_transitions_nochevrons_or_key1(pre, post) OR
      (per_key1(pre) AND ((fndisp1(pre) /= fok) AND (topline(pre) /= clearsetup)) AND
           (post = key1(pre))))) IMPLIES
        ((filter_rate(pre) = filter_rate(post)) AND
         (filter_vtbi(pre) = filter_vtbi(post)) AND
         (filter_time(pre) = filter_time(post)))

%%
  % @function filter_newvtbi
  % @description extract the temporary vtbi from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
filter_newvtbi(st: state): ivols = newvtbi(st)

%%
  % @function guard_fnkeyok
  % @description key1 permitted with okay and entrymode vtmode or ttmode
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_fnkeyok_vt(st: state): bool = per_key1(st) AND (fndisp1(st) = fok)
                        AND ((entrymode(st) = ttmode) OR (entrymode(st) = vtmode))

%%
% @theorem consistent_fnkeyok_vt_theorem
% @description ok with key1 consistently updates vtbi to temporary value
% @yields QED
% @stats Run time  = 0.91 secs. 20/7/17
%%
consistent_fnkeyok_vt_theorem: THEOREM
   FORALL(st: state):
       guard_fnkeyok_vt(st) IMPLIES
              (filter_newvtbi(st) = filter_vtbi(key1(st)))



%%
  % @function filter_newtime
  % @description extract the temporary time from the system
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
filter_newtime(st: state): itimes = newtime(st)

%%
  % @function guard_ok_ttmode
  % @description key1 permitted with okay and entrymode ttmode
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_ok_ttmode(st: state): bool = per_key1(st) AND (fndisp1(st) = fok) AND (entrymode(st) = ttmode)

%%
% @theorem consistent_ok_ttmode_theorem
% @description ok with key1 consistently updates time to temporary value
% @yields QED
% @stats Run time  = 0.87 secs. 20/7/17
%%
consistent_ok_ttmode_theorem: THEOREM
   FORALL(st: state):
       guard_ok_ttmode(st) IMPLIES
              (filter_newtime(st) = filter_time(key1(st)))

%%
  % @function guard_em_ok
  % @description key1 permitted with okay parameterised by entrymode
  % @param {emodes} em The entrymode
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_em_ok(em: emodes, st: state): boolean =
   per_key1(st) AND (fndisp1(st) = fok) AND (entrymode(st) = em) AND
   (entrymode(st) /= vttmode)

%%
  % @function temp_mode_filter
  % @description extract the relevant temporary value depending on entrymode
  % @param {emodes} em The entrymode
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
temp_mode_filter(em: emodes, st: state): real =
    COND
     em = rmode -> device(st)`infusionrate,
     em = infusemode -> device(st)`infusionrate,
     em = vtmode -> newvtbi(st),
     em = vttmode -> newvtbi(st),
     (em = bagmode OR em = tbagmode) ->
         COND bagscursor(st) = 0 -> 0,
              bagscursor(st) = 1 -> 50,
              bagscursor(st) = 2 -> 100,
              bagscursor(st) = 3 -> 200,
              bagscursor(st) = 4 -> 250,
              bagscursor(st) = 5 -> 500,
              bagscursor(st) = 6 -> 1000,
              bagscursor(st) = 7 -> 1500,
              bagscursor(st) = 8 -> 2000,
              ELSE -> 3000
         ENDCOND,
     em = ttmode -> newtime(st),
     ELSE -> device(st)`infusionrate
    ENDCOND

%%
  % @function real_mode_filter
  % @description extract the relevant actual paramter depending on entrymode
  % @param {emodes} em The entrymode
  % @param {state} st The state of the system
  % @returns {ivols}
  % @memberof module:alaris_consistency
  %%
real_mode_filter(em: emodes, st: state): real =
    COND
     em = rmode -> device(st)`infusionrate,
     em = rmode -> device(st)`infusionrate,
     em = vtmode -> device(st)`vtbi,
     em = vttmode -> device(st)`vtbi,
     em = bagmode -> newvtbi(st),
     em = tbagmode -> newvtbi(st),
     em = ttmode -> device(st)`time,
     ELSE -> device(st)`infusionrate
    ENDCOND

%%
% @theorem consistency_ok_em
% @description ok with key1 consistently updates parameter to temporary parameter depending on mmode
% @yields QED
% @stats Run time  = 1.31 secs. 20/7/17
%%
  consistency_ok_em: THEOREM
    FORALL (em: emodes, st: state):
      guard_em_ok(em, st) IMPLIES
          (temp_mode_filter(em, st) = real_mode_filter(em, key1(st)))



%%
  % @function vis_dispvtbi
  % @description predicate that is true if topline shows dispvtbi
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
vis_dispvtbi(st: state): boolean = (topline(st) = dispvtbi)

%%
  % @function vis_vtbitime
  % @description predicate that is true if topline shows vtbitime
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
vis_vtbitime(st: state): boolean = (topline(st) = vtbitime)

%%
% @theorem dispvtbimode_unchanged_parameters
% @description no action that leaves top line dispvtbi changes the pump parameters
% @yields QED
% @stats Run time  = 43.31 secs. 20/7/17
%%
dispvtbimode_unchanged_parameters: THEOREM
  FORALL(pre, post: state):
       (paused_state(pre) AND state_transitions(pre, post) AND
        vis_dispvtbi(pre) AND vis_dispvtbi(post)) IMPLIES
              ((filter_rate(pre) = filter_rate(post)) AND
               (filter_vtbi(pre) = filter_vtbi(post)) AND
               (filter_time(pre) = filter_time(post)))

%%
% @theorem vtbitimemode_unchanged_parameters
% @description no action that leaves top line vtbitime changes the pump parameters
% @yields QED
% @stats Run time  = 93.33 secs. 20/7/17
%%
vtbitimemode_unchanged_parameters: THEOREM
  FORALL(pre, post: state):
       (paused_state(pre) AND state_transitions(pre, post) AND
        vis_vtbitime(pre) AND vis_vtbitime(post)) IMPLIES
              ((filter_rate(pre) = filter_rate(post)) AND
               (filter_vtbi(pre) = filter_vtbi(post)) AND
               (filter_time(pre) = filter_time(post)))



%%
  % @function guard_em_quit
  % @description key3 permitted with quit parameterised by entrymode
  % @param {emodes} em The entrymode
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
guard_em_quit(em: emodes, st: state): boolean =
     per_key3(st) AND (fndisp3(st) = fquit) AND (entrymode(st) = em)

%%
% @theorem consistency_quit_em
% @description quit with key3 consistently leaves pump parameters unchanged
% @yields QED
% @stats Run time  = 1.87 secs. 20/7/17
%%
consistency_quit_em: THEOREM
    FORALL (em: emodes, st: state):
       guard_em_quit(em, st) IMPLIES
          real_mode_filter(em, st) = real_mode_filter(em, key3(st))

%%
  % @function f3quit_invariant
  % @description Quit only appears associated with key3
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
%
f3quit_invariant(st: state): boolean =
    fndisp1(st) /= fquit AND fndisp2(st) /= fquit

%%
% @theorem f3quit_consistent_theorem
% @description quit is only associated with key3
% @yields QED
% @stats Run time  = 139.27 secs. 20/7/17
%%
f3quit_consistent_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => f3quit_invariant(pre)) AND
     (state_transitions(pre, post) AND f3quit_invariant(pre) =>
                f3quit_invariant(post))

%%
  % @function f1ok_invariant
  % @description OK only appears associated with key1
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
f1ok_invariant(st: state): boolean =
    fndisp2(st) /= fok AND fndisp3(st) /= fok

%%
% @theorem f1ok_consistent_theorem
% @description ok is only associated with key1
% @yields QED
% @stats Run time  = 151.13 secs. 20/7/17
%%
f1ok_consistent_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => f1ok_invariant(pre)) AND
     (state_transitions(pre, post) AND f1ok_invariant(pre) =>
                f1ok_invariant(post))

%%
  % @function alwayskey1_invariant
  % @description  key1 limited to a subset of function displays
  % @param {state} st The state of the system
  % @returns {boolean}
  % @memberof module:alaris_consistency
  %%
alwayskey1_invariant(st: state): boolean =
    (fndisp1(st) /= fnull) <=>
    ((fndisp1(st) = fok) OR (fndisp1(st) = fclear) OR
     (fndisp1(st) = fvol) OR (fndisp1(st) = fyes))

%%
% @theorem always_key1_invariant_theorem
% @description  key1 is only associated with a subset of displays
% @yields QED
% @stats Run time  = 236.97 secs. 20/7/17
%%
alwayskey1_consistent_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => alwayskey1_invariant(pre)) AND
     (state_transitions(pre, post) AND alwayskey1_invariant(pre) =>
                alwayskey1_invariant(post))

END alaris_consistency_pattern</code></pre>
        </article>
    </section>
</div>
<nav>
    <h2><a href="index.html">Alaris GP</a></h2>
    <h3>Modules</h3>
    <ul>
        <li><a href="module-alaris_th.html">alaris_th</a></li>
        <li><a href="module-pump_th.html">pump_th</a></li>
        <li><a href="module-types_and_constants_th.html">types_and_constants_th</a></li>
    </ul>
    <h3>Theorems</h3>
    <ul>
        <li><a href="theorems-alaris_completeness.html">alaris_completeness</a></li>
        <li><a href="theorems-alaris_consistency.html">alaris_consistency</a></li>
        <li><a href="theorems-alaris_feedback.html">alaris_feedback</a></li>
        <li><a href="theorems-alaris_reversibility.html">alaris_reversibility</a></li>
        <li><a href="theorems-alaris_universality.html">alaris_universality</a></li>
        <li><a href="theorems-alaris_visibility.html">alaris_visibility</a></li>
    <ul>
</nav>
<br class="clear">
<footer>
    Documentation generated by <a href="https://github.com/jsdoc3/jsdoc">JSDoc 3.6.0-dev</a> on Sat Aug 26 2017 18:40:28 GMT+0100 (WEST)
</footer>
<script> prettyPrint(); </script>
<script src="scripts/linenumber.js"> </script>
</body>
</html>
