<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>Source: alaris_visibility.js</title>
    <script src="scripts/prettify/prettify.js"> </script>
    <script src="scripts/prettify/lang-css.js"> </script>
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link type="text/css" rel="stylesheet" href="styles/prettify-tomorrow.css">
    <link type="text/css" rel="stylesheet" href="styles/jsdoc-default.css">
</head>
<body>
    <div id="main">
        <h1 class="page-title">Source: alaris_visibility.js</h1>
        <section>
            <article>
                <pre class="prettyprint source linenums"><code>%%
% @module alaris_visibility
% @author Michael Harrison and Paolo Masci (QMUL, UMinho Braga and Newcastle University)
% @date April 2017
% @description The visibility theorems
%%
alaris_visibility_pattern[
maxrate  : posnat, %{maxrate: posnat | maxrate > 1000},
   maxinfuse: posnat, %{maxinfuse: posnat |  maxinfuse >= 3000 }, % maxtime = maxinfuse needed for proving maxinfuse_over_time_lemma and vtbi_over_time_lemma
   infusemin: { infusemin: posreal | infusemin <= maxinfuse },
   timeout  : posnat,
   shorttimeout : {shorttimeout: posnat | shorttimeout < timeout},
   maxtime  : { maxtime: posnat | maxtime > timeout AND maxtime >= maxinfuse },
   bat_max : {bat_max: posnat | bat_max+1 < maxtime},
   bat_min: {bat_min: posnat | bat_min < bat_max}
]: THEORY
BEGIN
  ASSUMING
    timeoutLTmaxtime: ASSUMPTION timeout < maxtime
    shorttimeoutLTtimeout: ASSUMPTION shorttimeout < timeout
    maxinfuseVal: ASSUMPTION maxinfuse >= 3000
  ENDASSUMING
  IMPORTING alaris_th[maxrate, maxinfuse, infusemin, timeout, shorttimeout, maxtime, bat_max, bat_min]

% Visibility template
% \forall s1, s2 in S:  transit(s1, s2) &
% visible(s1) => visible(s2)
% where
% visible(s) :=  filter(s) = p_filter_p(s)

state_transitions(pre, post: state): boolean =
    (per_sup(pre) & post = sup(pre))
    OR (per_release_sup(pre) & post = release_sup(pre))
    OR (per_fup(pre) & post = fup(pre))
    OR (per_release_fup(pre) & post = release_fup(pre))
    OR (per_sdown(pre) & post = sdown(pre))
    OR (per_release_sdown(pre) & post = release_sdown(pre))
    OR (per_fdown(pre) & post = fdown(pre))
    OR (per_release_fdown(pre) & post = release_fdown(pre))
    OR (per_tick(pre) & post = tick(pre))
    OR (per_key1(pre) & post = key1(pre))
    OR (per_key2(pre) & post = key2(pre))
    OR (per_key3(pre) & post = key3(pre))
    OR (per_query(pre) & post = query(pre))
    OR (per_on(pre) & post = on(pre))
    OR (per_run(pre) & post = run(pre))
    OR (per_pause(pre) & post = pause(pre))
    OR (per_switch(pre) & post = switch(pre))
    OR (per_fit_set(pre) & post = fit_set(pre))


%%
  % @function vtbi_entry_ready
  % @description circumstances when ready to enter vtbi
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtbi_entry_ready(st: state): boolean =
                 device(st)`powered_on? AND
                  ((topline(st) = dispvtbi AND entrymode(st) = vtmode) OR
                   (topline(st) = vtbitime AND entrymode(st) = vttmode))

%%
  % @function dispvtbi_entry_ready
  % @description limits circumstances when ready to enter vtbi to the vtmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
dispvtbi_entry_ready(st: state): boolean =
                  device(st)`powered_on? AND
                  (topline(st) = dispvtbi AND entrymode(st) = vtmode)

%%
  % @function vtbitime_entry_ready
  % @description limits circumstances when ready to enter vtbi to the vttmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtbitime_entry_ready(st: state): boolean =
                  device(st)`powered_on? AND
                  NOT device(st)`infusing? AND
                   (topline(st) = vtbitime AND entrymode(st) = vttmode)

%%
  % @function time_entry_ready
  % @description circumstances when ready to enter time
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
time_entry_ready(st: state): boolean =
                  device(st)`powered_on? AND
                  NOT device(st)`infusing? AND
                   (topline(st) = vtbitime AND entrymode(st) = ttmode)

%%
  % @function rate_entry_ready
  % @description circumstances when ready to enter infusion rate
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
rate_entry_ready(st: state): boolean =
    device(st)`powered_on? AND
     NOT rlock(st) AND
      ((entrymode(st) = rmode) OR (entrymode(st) = infusemode))

rate_entry_ready_locked(st: state): boolean =
    device(st)`powered_on? AND
     rlock(st) AND
      (((entrymode(st) = rmode) AND (topline(st) = holding)) OR
        ((entrymode(st) = infusemode) AND (topline(st) = infusing)))

univ_guard(st: state): boolean = true

paused(st: state): boolean =
    device(st)`powered_on? AND NOT device(st)`infusing?

infusing_state(st: state): boolean =
    device(st)`powered_on? AND device(st)`infusing?

%%
  % @function holding_guard
  % @description set of top lines not included because they are also possible when infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
holding_guard(st: state): boolean = (topline(st) /= locked) AND
                                    (topline(st) /= batterylow) AND
				    (topline(st) /= volume) AND
				    (topline(st) /= dispvtbi) AND
				    (topline(st) /= options) AND
				    (topline(st) /= dispinfo)

%%
  % @function infusing_guard
  % @description set of top lines not included because they are also possible when holding
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusing_guard(st: state): boolean = holding_guard(st)

%%
  % @function holding_filter
  % @description re-expresses pause for the template
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
holding_filter(st:state): boolean = paused(st)

%%
  % @function infusing_filter
  % @description re-expresses infusing for the template
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusing_filter(st: state): boolean = infusing_state(st)

%%
  % @function holding_p_filter
  % @description the disjunction of visible toplines for paused state
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
holding_p_filter(st: state): boolean =
     (topline(st) = holding OR topline(st) = setvtbi OR
      topline(st) = setrate OR topline(st) = vtbitime OR
      topline(st) = clearsetup OR topline(st) = attention OR
      topline(st) = lockrate OR topline(st) = setnotfitted)

%%
  % @function infusing_p_filter
  % @description the disjunction of visible toplines for infusing state
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusing_p_filter(st: state): boolean =
      (topline(st) = infusing) OR (topline(st) = dispkvo) OR
      (topline(st) = vtbidone)

%%
  % @function holding_visibility
  % @description predicate that requires specific toplines to associated with holding
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
holding_visibility(st: state): boolean =
      holding_guard(st) IMPLIES
         (holding_filter(st) = holding_p_filter(st))

%%
  % @function infusing_visibility
  % @description predicate that requires specific toplines to associated with infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusing_visibility(st: state): boolean =
      infusing_guard(st) IMPLIES
         (infusing_filter(st) = infusing_p_filter(st))

%%
% @theorem holding_visibility_theorem
% @description proves that given the guard, a set of top lines indicate holding
% @yields QED
% @stats Run time  = 625.93 secs. 26/7/17
%%
holding_visibility_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => holding_visibility(pre)) AND
     (state_transitions(pre, post) AND holding_visibility(pre)
          => holding_visibility(post))

%%
% @theorem infusing_visibility_theorem
% @description proves that a set of top lines indicate infusing
% @yields QED
% @stats Run time  = 416.69 secs. 26/7/17
%%
infusing_visibility_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => infusing_visibility(pre)) AND
     (state_transitions(pre, post) AND infusing_visibility(pre)
          => infusing_visibility(post))


%%
  % @function run_filter
  % @description predicate that is true if infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
run_filter(st: state): boolean =
    device(st)`powered_on? AND device(st)`infusing?

%%
  % @function run_p_filter
  % @description predicate that is true if the run light is switched on and the pause light is off
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
run_p_filter(st: state): boolean =
     runlight(st) AND (NOT pauselight(st))
%%
  % @function pause_filter
  % @description predicate that is true if the device is paused
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
pause_filter(st: state): boolean =
     device(st)`powered_on? AND (NOT device(st)`infusing?)

%%
  % @function pause_p_filter
  % @description predicate that is true if pause light is on and the run light is off
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
pause_p_filter(st: state): boolean =
      NOT runlight(st) AND pauselight(st)

%%
  % @function ac_on_filter
  % @description predicate that is true if powered on and ac connected
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
ac_on_filter(st: state): boolean =
      device(st)`powered_on? & device(st)`ac_connect

%%
  % @function ac_on_p_filter
  % @description predicate that is true if ac light is on
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
ac_on_p_filter(st: state): boolean =
      ac_light(st)

%%
  % @function battery_on_filter
  % @description predicate that is true if powered on and ac not connected
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
battery_on_filter(st: state): boolean =
      device(st)`powered_on? & NOT device(st)`ac_connect

%%
  % @function battery_on_p_filter
  % @description predicate that is true if the battery light is on
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
battery_on_p_filter(st: state): boolean =
       battery_light(st)

%%
  % @function visible_run
  % @description predicate that is true if run_filter is true iff run_p_filter true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_run(st: state): boolean =
    run_filter(st) = run_p_filter(st)

%%
  % @function visible_pause
  % @description predicate that is true if pause_filter is true iff pause_p_filter true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_pause(st: state): boolean =
    pause_filter(st) = pause_p_filter(st)

%%
  % @function visible_ac_on
  % @description predicate that is true if ac_on_filter is true iff ac_on_p_filter true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_ac_on(st: state): boolean =
     ac_on_filter(st) = ac_on_p_filter(st)

%%
  % @function visible_battery_on
  % @description predicate that is true if battery_on_filter is true iff battery_on_p_filter true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_battery_on(st:state): boolean =
     battery_on_filter(st) = battery_on_p_filter(st)


%%
% @theorem visible_run_theorem
% @description proves that the appropriate lights indicate that the device is infusing
% @yields QED
% @stats Run time  = 176.89 secs. 26/7/17
%%
visible_run_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_run(pre)) AND
       ((state_transitions(pre, post) AND visible_run(pre))
                  => visible_run(post))

%%
% @theorem visible_pause_theorem
% @description proves that the appropriate lights indicate that the device is paused
% @yields QED
% @stats Run time  = 254.31 secs. 26/7/17
%%
visible_pause_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_pause(pre)) AND
       ((state_transitions(pre, post) AND visible_pause(pre))
                  => visible_pause(post))

%%
% @theorem visible_ac_on_theorem
% @description proves that the appropriate lights indicate that the device is switched on
% @yields QED
% @stats Run time  = 184.29 secs. 26/7/17
%%
visible_ac_on_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_ac_on(pre)) AND
       ((state_transitions(pre, post) AND visible_ac_on(pre))
                  => visible_ac_on(post))

%%
% @theorem visible_battery_on_theorem
% @description proves that the appropriate lights indicate that the device is on battery
% @yields QED
% @stats Run time  = 243.53 secs. 26/7/17
%%
visible_battery_on_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_battery_on(pre)) AND
       ((state_transitions(pre, post) AND visible_battery_on(pre))
                  => visible_battery_on(post))

%%
  % @function guard_vis_rmode
  % @description top line not locked
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_rmode(st: state): boolean =
        topline(st) /= locked

%%
  % @function rmode_filter
  % @description entry mode is rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
rmode_filter(st: state): boolean = entrymode(st) = rmode

%%
  % @function rmode_p_filter
  % @description top line is holding or top line is set rate
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
rmode_p_filter(st: state): boolean =
        (topline(st) = holding) OR (topline(st) = setrate)

%%
  % @function visible_rmode
  % @description visibility property relating to rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_rmode(st:state): boolean =
   guard_vis_rmode(st) IMPLIES
     (rmode_filter(st) = rmode_p_filter(st))

%%
% @theorem visible_rmode_theorem
% @description proves that the visibility of rmode
% @yields QED
% @stats Run time  = 359.42 secs. 26/7/17
%%
visible_rmode_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_rmode(pre)) AND
       ((state_transitions(pre, post) AND visible_rmode(pre))
                  => visible_rmode(post))

%%
  % @function guard_vis_infusemode
  % @description top line not locked
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_infusemode(st: state): boolean =
        topline(st) /= locked

%%
  % @function infusemode_filter
  % @description entrymode is infusemode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusemode_filter(st: state): boolean = entrymode(st) = infusemode

%%
  % @function infusemode_p_filter
  % @description top line is infusing or dispkvo
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusemode_p_filter(st: state): boolean =
        (topline(st) = infusing) OR (topline(st) = dispkvo)

%%
  % @function visible_infusemode
  % @description visibility property for infusing
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_infusemode(st:state): boolean =
   guard_vis_infusemode(st) IMPLIES
     (infusemode_filter(st) = infusemode_p_filter(st))

%%
% @theorem visible_infusemode_theorem
% @description visibility of infusemode
% @yields QED
% @stats Run time  = 461.20 secs. 26/7/17
%%
visible_infusemode_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_infusemode(pre)) AND
       ((state_transitions(pre, post) AND visible_infusemode(pre))
                  => visible_infusemode(post))

%%
  % @function guard_vis_kvo
  % @description true, ie no guard
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_kvo(st: state): boolean = true
 %       topline(st) /= locked


%%
  % @function kvo_filter
  % @description infusemode with the kvoflag set
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
kvo_filter(st: state): boolean =
       entrymode(st) = infusemode AND device(st)`kvoflag

%%
  % @function kvo_p_filter
  % @description topline shows kvo
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
kvo_p_filter(st: state): boolean =
        (topline(st) = dispkvo)

%%
  % @function visible_kvo
  % @description the visibility property relating to kvo
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_kvo(st:state): boolean =
   guard_vis_kvo(st) IMPLIES
     (kvo_filter(st) = kvo_p_filter(st))

%%
% @theorem visible_kvo_theorem
% @description visibility of kvo mode
% @yields QED
% @stats Run time  = 366.25 secs. 26/7/17
%%
Visible_kvo_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_kvo(pre)) AND
       ((state_transitions(pre, post) AND visible_kvo(pre))
                  => visible_kvo(post))

%%
  % @function guard_vis_dispvtbi
  % @description true, ie no guard
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_dispvtbi(st: state): boolean = true

%%
  % @function dispvtbi_filter
  % @description entrymodes that can be related to top line of dispvtbi
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
dispvtbi_filter(st: state): boolean =
       entrymode(st) = vtmode OR
       entrymode(st) = bagmode OR
       entrymode(st) = tbagmode

%%
  % @function dispvtbi_p_filter
  % @description topline dispvtbi
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
dispvtbi_p_filter(st: state): boolean =
        topline(st) = dispvtbi

%%
  % @function visible_dispvtbi
  % @description the modes are visible via the top line
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_dispvtbi(st:state): boolean =
   guard_vis_dispvtbi(st) IMPLIES
     (dispvtbi_filter(st) = dispvtbi_p_filter(st))

%%
% @theorem visible_dispvtbi_theorem
% @description visibility of vtbi modes
% @yields QED
% @stats Run time  = 500.44 secs. 26/7/17
%%
visible_dispvtbi_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_dispvtbi(pre)) AND
       ((state_transitions(pre, post) AND visible_dispvtbi(pre))
                  => visible_dispvtbi(post))

%%
  % @function guard_vis_vtbitime
  % @description true, ie no guard
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_vtbitime(st: state): boolean = true

%%
  % @function vtbitime_filter
  % @description entry modes associated with vtbitime
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtbitime_filter(st: state): boolean =
       entrymode(st) = vttmode OR
       entrymode(st) = ttmode

%%
  % @function vtbitime_p_filter
  % @description topline vtbitime
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtbitime_p_filter(st: state): boolean =
        topline(st) = vtbitime

%%
  % @function visible_vtbitime
  % @description the modes are visible via the top line
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_vtbitime(st:state): boolean =
   guard_vis_vtbitime(st) IMPLIES
     (vtbitime_filter(st) = vtbitime_p_filter(st))

%%
% @theorem visible_vtbitime_theorem
% @description visibility of vtbi over time modes
% @yields QED
% @stats Run time  = 405.82 secs. 26/7/17
%%
visible_vtbitime_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_vtbitime(pre)) AND
       ((state_transitions(pre, post) AND visible_vtbitime(pre))
                  => visible_vtbitime(post))

%%
  % @function guard_vis_nullmode
  % @description true, ie no guard
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_nullmode(st: state): boolean = true

%%
  % @function nullmode_filter
  % @description entry modes associated with nullmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
nullmode_filter(st: state): boolean =
       entrymode(st) = nullmode AND device(st)`powered_on?

%%
  % @function nullmode_p_filter
  % @description set of toplines associated with nullmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
nullmode_p_filter(st: state): boolean =
        topline(st) = clearsetup OR
        topline(st) = vtbidone OR
        topline(st) = setvtbi OR
        topline(st) = attention OR
        topline(st) = volume OR
        topline(st) = dispinfo OR
        topline(st) = batterylow OR
        topline(st) = lockrate OR
        topline(st) = setnotfitted

%%
  % @function visible_nullmode
  % @description the mode is visible via the top line
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_nullmode(st:state): boolean =
   guard_vis_nullmode(st) IMPLIES
     (nullmode_filter(st) = nullmode_p_filter(st))

%%
% @theorem visible_nullmode_theorem
% @description visibility of nullmode
% @yields QED
% @stats Run time  = 1156.71 secs. 27/7/17
%%
visible_nullmode_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_nullmode(pre)) AND
       ((state_transitions(pre, post) AND visible_nullmode(pre))
                  => visible_nullmode(post))

%%
  % @function guard_vis_mode
  % @description true if infusion rate not locked
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_qmode(st: state): boolean = NOT rlock(st)

%%
  % @function qmode_filter
  % @description entry mode is qmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
qmode_filter(st: state): boolean =
       entrymode(st) = qmode

%%
  % @function qmode_p_filter
  % @description topline is options - ie options menu
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
qmode_p_filter(st: state): boolean =
        topline(st) = options

%%
  % @function visible_qmode
  % @description the mode is visible via the top line
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_qmode(st:state): boolean =
   guard_vis_qmode(st) IMPLIES
     (qmode_filter(st) = qmode_p_filter(st))

%%
% @theorem visible_qmode_theorem
% @description visibility of qmode
% @yields QED
% @stats Run time  = 291.96 secs. 27/7/17
%%
visible_qmode_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_qmode(pre)) AND
       ((state_transitions(pre, post) AND visible_qmode(pre))
                  => visible_qmode(post))

%%
  % @function guard_vis_mode_rate
  % @description true if infusion rate not locked
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_rmode_rate(st: state): boolean = NOT rlock(st)

%%
  % @function rmode_rate_filter
  % @description entry mode is rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
rmode_rate_filter(st: state): boolean =
       entrymode(st) = rmode

%%
  % @function rmode_rate_p_filter
  % @description the infusion pump rate variable is visible
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
rmode_rate_p_filter(st: state): boolean =
        middisp(st)(drate)

%%
  % @function part_visible_rmode_rate
  % @description rate is visible if entrymode is rmode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
part_visible_rmode_rate(st:state): boolean =
   (guard_vis_rmode_rate(st) AND
     rmode_rate_filter(st)) => rmode_rate_p_filter(st)

%%
% @theorem visible_rmode_rate_theorem
% @description visibility of infusion rate in rmode
% @yields QED
% @stats Run time  = 171.37 secs. 27/7/17
%%
visible_rmode_rate_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => part_visible_rmode_rate(pre)) AND
       ((state_transitions(pre, post) AND part_visible_rmode_rate(pre))
                  => part_visible_rmode_rate(post))

%%
  % @function guard_vis_infusemode_rate
  % @description true if infusion rate not locked
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_infusemode_rate(st: state): boolean = NOT rlock(st)

%%
  % @function infusemode_rate_filter
  % @description entry mode is infusemode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusemode_rate_filter(st: state): boolean =
       entrymode(st) = infusemode

%%
  % @function infusemode_rate_p_filter
  % @description the infusion pump rate variable is visible or kvorate if vtbi is exhausted
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
infusemode_rate_p_filter(st: state): boolean =
        middisp(st)(drate) OR middisp(st)(dkvorate)

%%
  % @function part_visible_infusemode_rate
  % @description rate or kvorate is visible if entrymode is infusemode
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
part_visible_infusemode_rate(st:state): boolean =
   (guard_vis_infusemode_rate(st) AND
     infusemode_rate_filter(st)) => infusemode_rate_p_filter(st)

%%
% @theorem visible_infusemode_rate_theorem
% @description visibility of infusion rate in rmode
% @yields QED
% @stats Run time  = 210.93 secs. 27/7/17
%%
visible_infusemode_rate_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => part_visible_infusemode_rate(pre)) AND
       ((state_transitions(pre, post) AND part_visible_infusemode_rate(pre))
                  => part_visible_infusemode_rate(post))

%%
  % @function guard_vis_vtvttmode_vtbi
  % @description true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_vtvttmode_vtbi(st: state): boolean = true

%%
  % @function vtvttmode_vtbi_filter
  % @description entry mode is disjunction of vtbi entry modes
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtvttmode_vtbi_filter(st: state): boolean =
       (entrymode(st) = vtmode) OR
       (entrymode(st) = vttmode) OR
       (entrymode(st) = ttmode)

%%
  % @function vtvttmode_vtbi_p_filter
  % @description the temporary value of vtbi is visible
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtvttmode_vtbi_p_filter(st: state): boolean =
        middisp(st)(dnewvtbi)



%%
  % @function visible_vtvttmode_vtbi
  % @description temp variable vtbi visible in vtbi entry modes
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_vtvttmode_vtbi(st:state): boolean =
   guard_vis_vtvttmode_vtbi(st) =>
     (vtvttmode_vtbi_filter(st) = vtvttmode_vtbi_p_filter(st))

%%
% @theorem visible_vtvttmode_vtbi_theorem
% @description visibility of temporary vtbi in the vtbi emodes
% @yields QED
% @stats Run time  = 233.46 secs. 27/7/17
%%
visible_vtvttmode_vtbi_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_vtvttmode_vtbi(pre)) AND
       ((state_transitions(pre, post) AND visible_vtvttmode_vtbi(pre))
                  => visible_vtvttmode_vtbi(post))

%%
  % @function guard_vis_vtttmode_time
  % @description true
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
guard_vis_vtttmode_time(st: state): boolean = true

%%
  % @function vtttmode_time_filter
  % @description entry mode is disjunction of vtbi over time entry modes
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtttmode_time_filter(st: state): boolean =
       (entrymode(st) = vttmode) OR
       (entrymode(st) = ttmode)

%%
  % @function vtttmode_time_p_filter
  % @description the temporary value of time is visible
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
vtttmode_time_p_filter(st: state): boolean =
        middisp(st)(dnewtime)

%%
  % @function visible_vtttmode_time
  % @description temp variable time visible in vtbi over time entry modes
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
visible_vtttmode_time(st:state): boolean =
   guard_vis_vtttmode_time(st) =>
     (vtttmode_time_filter(st) = vtttmode_time_p_filter(st))

%%
% @theorem visible_vtttmode_time_theorem
% @description visibility of temporary time in the vtbi over time emodes
% @yields QED
% @stats Run time  = 143.09 secs. 27/7/17
%%
visible_vtttmode_time_theorem: THEOREM
   FORALL (pre, post: state):
       (init?(pre) => visible_vtttmode_time(pre)) AND
       ((state_transitions(pre, post) AND visible_vtttmode_time(pre))
                  => visible_vtttmode_time(post))

%%
  % @function pred_off_filter
  % @description power off
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
pred_off_filter(st: state): boolean =
          NOT device(st)`powered_on?

%%
  % @function pred_p_off_filter
  % @description the temporary value of time is visible
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
pred_p_off_filter(st: state): boolean =
          topline(st) = dispblank


%%
  % @function off_visibility
  % @description when device off, topline is blank
  % @param {state} st The state of the system
  % @returns {bool}
  % @memberof module:alaris_visibility
  %%
off_visibility(st: state): boolean =
   (pred_off_filter(st) = pred_p_off_filter(st))

%%
% @theorem off_visibility_theorem
% @description topline blank when device off
% @yields QED
% @stats Run time  = 179.48 secs. 27/7/17
%%
off_visibility_theorem: THEOREM
FORALL (pre, post: state):
    (init?(pre)  => off_visibility(pre)) AND
     (state_transitions(pre, post) AND off_visibility(pre)
          => off_visibility(post))


END alaris_visibility_pattern</code></pre>
            </article>
        </section>
    </div>
    <nav>
        <h2><a href="index.html">Alaris GP</a></h2>
        <h3>Modules</h3>
        <ul>
            <li><a href="module-alaris_th.html">alaris_th</a></li>
            <li><a href="module-pump_th.html">pump_th</a></li>
            <li><a href="module-types_and_constants_th.html">types_and_constants_th</a></li>
        </ul>
        <h3>Theorems</h3>
        <ul>
            <li><a href="theorems-alaris_completeness.html">alaris_completeness</a></li>
            <li><a href="theorems-alaris_consistency.html">alaris_consistency</a></li>
            <li><a href="theorems-alaris_feedback.html">alaris_feedback</a></li>
            <li><a href="theorems-alaris_reversibility.html">alaris_reversibility</a></li>
            <li><a href="theorems-alaris_universality.html">alaris_universality</a></li>
            <li><a href="theorems-alaris_visibility.html">alaris_visibility</a></li>
        <ul>
    </nav>
    <br class="clear">
    <footer>
        Documentation generated by <a href="https://github.com/jsdoc3/jsdoc">JSDoc 3.6.0-dev</a> on Sat Aug 26 2017 15:43:28 GMT+0100 (WEST)
    </footer>
    <script> prettyPrint(); </script>
    <script src="scripts/linenumber.js"> </script>
</body>
</html>
